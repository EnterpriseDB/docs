---

title: "CREATE TRIGGER"
---

<div id="create_trigger" class="registered_link"></div>

**Name**

`CREATE TRIGGER -`- define a new trigger

**Synopsis**

`CREATE [ OR REPLACE ] TRIGGER` *`name`*

  `{ BEFORE | AFTER | INSTEAD OF }`

  `{ INSERT | UPDATE | DELETE }`

​      `[ OR { INSERT | UPDATE | DELETE } ] [, ...]`

​    `ON` *`table`*

  `[ REFERENCING { OLD AS` *`old`* `| NEW AS` *`new`* `} ...]`

  `[ FOR EACH ROW ]`

  `[ WHEN` *`condition`* `]`

  `[ DECLARE`

​      `[ PRAGMA AUTONOMOUS_TRANSACTION; ]`

 *`declaration`*`; [, ...] ]`

​    `BEGIN`

 *`statement`*`; [, ...]`

  `[ EXCEPTION`

​    `{ WHEN` *`exception`* `[ OR` *`exception`* `] [...] THEN`

 *`statement`*`; [, ...] } [, ...]`

  `]`

​    `END`

**Description**

`CREATE TRIGGER` defines a new trigger. `CREATE OR REPLACE TRIGGER` will either create a new trigger, or replace an existing definition.

If you are using the `CREATE TRIGGER` keywords to create a new trigger, the name of the new trigger must not match any existing trigger defined on the same table. New triggers will be created in the same schema as the table on which the triggering event is defined.

If you are updating the definition of an existing trigger, use the `CREATE OR REPLACE TRIGGER` keywords.

When you use syntax that is compatible with Oracle to create a trigger, the trigger runs as a `SECURITY DEFINER` function.

**Parameters**

*`name`*

> The name of the trigger to create.

`BEFORE | AFTER`

> Determines whether the trigger is fired before or after the triggering event.

`INSERT | UPDATE | DELETE`

> Defines the triggering event.

*`table`*

> The name of the table on which the triggering event occurs.

*`condition`*

> *`condition`* is a Boolean expression that determines if the trigger will actually be executed; if *`condition`* evaluates to `TRUE`, the trigger will fire.
>
> If the trigger definition includes the `FOR EACH ROW` keywords, the `WHEN` clause can refer to columns of the old and/or new row values by writing `OLD.`*`column_name`* or `NEW.`*`column_name`* respectively. `INSERT` triggers cannot refer to `OLD` and `DELETE` triggers cannot refer to `NEW`.
>
> If the trigger includes the `INSTEAD` `OF` keywords, it may not include a `WHEN` clause. A `WHEN` clause cannot contain subqueries.

`REFERENCING { OLD AS` *`old`* `| NEW AS` *`new`* `} ...`

> `REFERENCING` clause to reference old rows and new rows, but restricted in that *old* may only be replaced by an identifier named old or any equivalent that is saved in all lowercase (for example, `REFERENCING OLD AS old`, `REFERENCING OLD AS OLD`, or `REFERENCING OLD AS "old`"). Also, *`new`* may only be replaced by an identifier named new or any equivalent that is saved in all lowercase (for example, `REFERENCING NEW AS new`, `REFERENCING NEW AS NEW`, or `REFERENCING NEW AS "new"`).
>
> Either one, or both phrases `OLD AS` *`old`* and `NEW AS` *`new`* may be specified in the REFERENCING clause (for example, REFERENCING NEW AS New OLD AS Old).
>
> This clause is not compatible with Oracle databases in that identifiers other than `old` or `new` may not be used.

`FOR EACH ROW`

> Determines whether the trigger should be fired once for every row affected by the triggering event, or just once per SQL statement. If specified, the trigger is fired once for every affected row (row-level trigger), otherwise the trigger is a statement-level trigger.

`PRAGMA AUTONOMOUS_TRANSACTION`

`PRAGMA AUTONOMOUS_TRANSACTION` is the directive that sets the trigger as an autonomous transaction.

*`declaration`*

A variable, type, `REF CURSOR`, or subprogram declaration. If subprogram declarations are included, they must be declared after all other variable, type, and `REF CURSOR` declarations.

*`statement`*

An SPL program statement. Note that a `DECLARE - BEGIN - END` block is considered an SPL statement unto itself. Thus, the trigger body may contain nested blocks.

*`exception`*

An exception condition name such as `NO_DATA_FOUND, OTHERS`, etc.

**Examples**

The following is a statement-level trigger that fires after the triggering statement (insert, update, or delete on table `emp`) is executed.

```
CREATE OR REPLACE TRIGGER user_audit_trig
    AFTER INSERT OR UPDATE OR DELETE ON emp
DECLARE
    v_action        VARCHAR2(24);
BEGIN
    IF INSERTING THEN
        v_action := ' added employee(s) on ';
    ELSIF UPDATING THEN
        v_action := ' updated employee(s) on ';
    ELSIF DELETING THEN
        v_action := ' deleted employee(s) on ';
    END IF;
    DBMS_OUTPUT.PUT_LINE('User ' || USER || v_action ||
        TO_CHAR(SYSDATE,'YYYY-MM-DD'));
END;
```

The following is a row-level trigger that fires before each row is either inserted, updated, or deleted on table `emp`.

```
CREATE OR REPLACE TRIGGER emp_sal_trig
    BEFORE DELETE OR INSERT OR UPDATE ON emp
    FOR EACH ROW
DECLARE
    sal_diff       NUMBER;
BEGIN
    IF INSERTING THEN
        DBMS_OUTPUT.PUT_LINE('Inserting employee ' || :NEW.empno);
        DBMS_OUTPUT.PUT_LINE('..New salary: ' || :NEW.sal);
    END IF;
    IF UPDATING THEN
        sal_diff := :NEW.sal - :OLD.sal;
        DBMS_OUTPUT.PUT_LINE('Updating employee ' || :OLD.empno);
        DBMS_OUTPUT.PUT_LINE('..Old salary: ' || :OLD.sal);
        DBMS_OUTPUT.PUT_LINE('..New salary: ' || :NEW.sal);
        DBMS_OUTPUT.PUT_LINE('..Raise     : ' || sal_diff);
    END IF;
    IF DELETING THEN
        DBMS_OUTPUT.PUT_LINE('Deleting employee ' || :OLD.empno);
        DBMS_OUTPUT.PUT_LINE('..Old salary: ' || :OLD.sal);
    END IF;
END;
```

See Also

[DROP TRIGGER](https://www.enterprisedb.com/edb-docs/d/edb-postgres-advanced-server/reference/database-compatibility-for-oracle-developers-reference-guide/11/Database_Compatibility_for_Oracle_Developers_Reference_Guide.1.073.html#)